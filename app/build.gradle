import se.bjurr.violations.comments.github.plugin.gradle.ViolationCommentsToGitHubTask
import se.bjurr.violations.lib.model.SEVERITY

apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: "se.bjurr.violations.violation-comments-to-github-gradle-plugin"


android {
  compileSdkVersion 26
  buildToolsVersion "26.0.0"
  defaultConfig {
    applicationId "com.simple.checkstyledemo"
    minSdkVersion 14
    targetSdkVersion 26
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }
  lintOptions {
    abortOnError true
    xmlReport true
    htmlReport false
    lintConfig file("${project.rootDir}/config/lint/lint.xml")
  }
}

task checkstyle(type: Checkstyle) {
  ignoreFailures true
  showViolations true
  configFile file("${project.rootDir}/config/checkstyle/android-style.xml")
  configProperties.checkstyleSuppressionsPath = file("${project.rootDir}/config/checkstyle/suppressions.xml")
  source 'src/main/java'
  include '**/*.java'
  exclude '**/gen/**'
  classpath = files()
  reports {
    xml.enabled true
    html.enabled false
  }
}

task findbugs(type: FindBugs) {
  ignoreFailures true
  effort "max"
  reportLevel "high"
  excludeFilter file("${project.rootDir}/config/findbugs/exclude.xml")
  source fileTree('src/main/java')
  classes = files("${project.buildDir}/intermediates/classes")
  classpath = files()
  reports {
    xml.enabled true
    html.enabled false
  }
}

task pmd(type: Pmd) {
  ignoreFailures true
  ruleSetFiles = files("${project.rootDir}/config/pmd/ruleset.xml")
  source 'src/main/java'
  include '**/*.java'
  exclude '**/gen/**'
  reports {
    xml.enabled true
    html.enabled false
  }
}

task violationCommentsToGitHub(type: ViolationCommentsToGitHubTask) {
  repositoryOwner "tuanbaros";
  repositoryName "checkstyledemo"
  pullRequestId System.properties['GITHUB_PULLREQUESTID']
  username System.properties['GITHUB_USERNAME']
  password System.properties['GITHUB_PASSWORD']
  oAuth2Token System.properties['GITHUB_OAUTH2TOKEN']
  gitHubUrl "https://api.github.com/"
  createCommentWithAllSingleFileComments false
  createSingleFileComments true
  commentOnlyChangedContent true
  minSeverity SEVERITY.INFO // ERROR, INFO, WARN
  violations = [
      ["FINDBUGS",   ".", ".*/findbugs/.*\\.xml\$",   "Findbugs"],
      ["PMD",        ".", ".*/pmd/.*\\.xml\$",        "PMD"],
      ["CHECKSTYLE", ".", ".*/checkstyle/.*\\.xml\$", "Checkstyle"],
      ["JSHINT",     ".", ".*/jshint/.*\\.xml\$",     "JSHint"],
      ["CSSLINT",    ".", ".*/csslint/.*\\.xml\$",     "CssLint"],
      ["LINT",    ".", ".*/.*\\.xml\$",     "Lint"]
  ]
}

dependencies {
  compile fileTree(dir: 'libs', include: ['*.jar'])
  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  compile 'com.android.support:appcompat-v7:26.0.0-alpha1'
  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  testCompile 'junit:junit:4.12'

  //check style
  checkstyle 'com.puppycrawl.tools:checkstyle:6.15'
}
